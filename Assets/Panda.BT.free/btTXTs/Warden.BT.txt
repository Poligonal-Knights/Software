tree("Root")
	while IsMyTurn
		fallback
			Sequence
				IsInOffense()
				tree "Trashmob"
			repeat
				fallback
					tree "ProtectedAlly"
					tree "ChooseProtectedAlly"
					tree "Offense"
			EndTurn

//There's a protected Ally
tree("ProtectedAlly")
	sequence
		IsAllyChosen()
		fallback
			tree "ProtectiveOffense"
			tree "Buffing"

tree("ProtectiveOffense")
	sequence
		EnemyInAllysRange()
		FocusedEnemyInRange()
		GetCloserEnemyFocused()
		Wait 1.0
		CanIAttack()
		Attack()
		GetCloserProtectedAlly()
		Wait 1.0

tree ("Buffing")
	sequence
		fallback
			sequence
				IsAllyInRange()
				GetCloserProtectedAlly()
				Wait 1.0
				DefenseBuff()
			sequence
				MovementBuff()
				GetCloserProtectedAlly()
				Wait 1.0
		EndTurn()

//Tree ChooseProtectedAlly
tree("ChooseProtectedAlly")
	not
		sequence
			IsAnyoneAlive()
			tree "PriorityAlly"

tree ("PriorityAlly")
	fallback
		sequence
			AlliesInRange()
			fallback
				IsThereAnyHealer()
				IsThereAnyMage()
				IsThereAnyArcher()
				IsThereAnyTrashmob()
		ChooseCloserAlly()

tree ("Offense")
	sequence
		ChangeToOffense()
		tree "Trashmob"


tree("Trashmob")
	while IsMyTurn
		fallback
			repeat
				sequence
					tree "FocusEnemy"
					tree "AttackAndMove"
			EndTurn

//Focus Focus Enemy
tree("FocusEnemy")
	fallback
		sequence
			IsEnemyFocused()
			IsEnemyAlive()
		sequence
			EnemiesInRange()
			fallback
				BattleCryActive()
				ChooseInjured()
		sequence		
			ChooseCloserEnemy()

//Tree Attack And Move
tree("AttackAndMove")
	fallback
		sequence
			CanIAttack()
			InAttackRange()
			Attack()
		sequence
			CanIMove()
			not
				InAttackRange()
			GetCloserEnemyFocused()
			Wait 1.0